! ---------------------------------------------------------------------------------
! STATIC_1: INTERNAL PRESSURE ANALYSIS
! ...............................................................
! PROBLEM:  DETERMINE STRAIN AND STRESS FIELD DUE TO INTERNAL PRESSURE IN CYLINDER
! ...............................................................
! MODEL:    HYBRID ELEMENTS SIMPLIFIED MODEL - 3D all round
! ---------------------------------------------------------------------------------
! version: v4_10/07

FINISH
/CLEAR, START, NEW
/FILNAM, static_pres
/TITLE, static internal pressure

!------------------------------------------------------------------------------------------------------------

! >>>>> MODEL PARAMETERS <<<<<

eps = 1e-4

cylinder_do = 165		! Cylinder outer diameter [mm]
cylinder_di = 120		! Cylinder inner diameter [mm]
cylinder_ho = 880		! Cylinder outer height   [mm]
cylinder_hi = 520		! Cylinder inner height   [mm]

solid_h    = 90			! Height of solid part of the piston   [mm]
solid_d    = 120        	! Diameter of solid part of the piston [mm]
piston_h   = 690		! Piston full height [mm]
piston_b   = 265		! Piston cantilever length (one side) [mm]

side_support_r = 190		! Distance from axis of support [mm]
side_support_h = 370		! Distance of support from base [mm]

esize_s1 = 10			! Solid mesh element size #1
esize_s2 = 20			! Solid mesh element size #2
esize_b  = 10			! Beam mesh size
ndiv1    = 10			! Lines divisions size
ndiv2    = 5
press    = 100			! Internal pressure value [Pa] ########TESTING VALUE#########

!------------------------------------------------------------------------------------------------------------

! >>>>> MATERIAL PROPERTIES <<<<<
/PREP7

! MATERIAL 1 -> High Res. Steel 4340M
MP,EX,1,210000
MP,PRXY,1,0.3

!------------------------------------------------------------------------------------------------------------

! >>>>> ELEMENT PROPERTIES <<<<<

! ELEMENT 1 -> SOLID185(for mapped) 187 (for free)
ET,1,SOLID187
!KEYOPT,1,

! ELEMENT 2 -> BEAM188
ET,2,BEAM188
KEYOPT,2,3,2		!Quadratic formulation to represent correctly linearly varying bending moments
SECTYPE,2,BEAM,CSOLID
SECDATA,solid_d*0.5

! ELEMENT 3 -> CONTACT BONDED
ET,3,CONTA174
KEYOPT,3,2,0		!Contact algorithm: Augmented Lagrangian (Default)
KEYOPT,3,12,5 		!Behavior of contact surface: Bonded (always)
KEYOPT,3,4,2 		!Rigid surface constraint

! ELEMENT 4 -> TARGET
ET,4,TARGE170
KEYOPT,4,2,1		!Boundary conditions for rigid target nodes: Specified by user (0=autoset)
R,1			!Real Constants set #1, All Default

! ELEMENT 5 -> CONTACT FRICTIONLESS
ET,5,CONTA174
KEYOPT,5,2,2		!Contact algorithm: Multipoint constraint (MPC)
KEYOPT,5,12,2		!Behavior of contact surface: No separation (Sliding Permitted)
KEYOPT,5,4,2 		!Rigid surface constraint
R,2

!------------------------------------------------------------------------------------------------------------

! >>>>> BUILD GEOMETRIES <<<<<

! Base beam
K,1,0,0,0
K,2,+piston_b,0,0
K,3,-piston_b,0,0
solid_base_y = piston_h - solid_h
K,4,0,solid_base_y,0

L,1,2
L,1,3
L,1,4

! Solid head
solid_r = solid_d*0.5

K,5,solid_r,solid_base_y,0
K,6,solid_r,solid_base_y+solid_h
K,7,0,solid_base_y+solid_h
L,4,5
L,5,6
L,6,7
L,7,4
LESIZE,4,esize_s1
LESIZE,5,esize_s1
LESIZE,6,esize_s1
LESIZE,7,esize_s1
! Area 1
AL,4,5,6,7

! Cylinder
K,8,cylinder_do*0.5,solid_base_y,0
K,9,cylinder_do*0.5,cylinder_hi+solid_base_y
K,10,cylinder_do*0.5,cylinder_ho+solid_base_y
K,11,0,cylinder_ho+solid_base_y
K,12,0,cylinder_hi+solid_base_y
K,13,cylinder_di*0.5,cylinder_hi+solid_base_y

! horizontals
L,5,8
L,8,9
L,9,10
L,10,11
L,11,12
L,12,13
L,13,5
ALLS

LSEL,S,LINE,,13,14,1
LESIZE,ALL,10
ALLS

! Area 2
AL,8,9,10,11,12,13,14

! Make volumes
VROTAT,1,,,,,,4,7
!VADD,1,2,3,4		! Merge volumes into one
!NUMCMP,VOLU		! Back to be #1

VROTAT,2,,,,,,4,11,,8
ALLS

!------------------------------------------------------------------------------------------------------------

! >>>>> MESH ELEMENTS <<<<<

! SOLID Elements
TYPE,1
ESIZE,esize_s1
MSHKEY,0
MSHAPE,1,3D 		!Tetra Elements if you are using solid187
!MSHKEY,1		!Free meshing (default), 1 for mapped
!MSHAPE,0,3D 		!Quadrilateral shaped elements ONLY FOR MAPPED MESH and 185
VSEL,S,VOLU,,1,4,1		!Select volumes that make up the piston head
VMESH,ALL

VSEL,INVE
ESIZE,esize_s2
!MSHKEY,0
!MSHAPE,1,3D
VMESH,ALL
ALLS
! Refined by hand on inner faces
!asel,s,area,,22,23,1
!asel,a,area,,29,30  
!asel,a,area,,36,37  
!asel,a,area,,43,44  
!asel,a,area,,50,51  
!asel,a,area,,57,58  
!asel,a,area,,64,65  
!asel,a,area,,71,72 

! ####### May need better mesh control/mapping ############

! CONTA on solid surface
TYPE,3
REAL,1				!Call real constants ID
CSYS,0
VSEL,S,VOLU,,1,4,1
NSLV,S,1
NSEL,R,LOC,Y,piston_h-solid_h-eps,piston_h-solid_h+eps
csys,5
nsel,r,loc,x,0,solid_r*0.75
CSYS,0
ESURF
ALLS

! PILOT node 
TYPE,4
REAL,1				!Call real constants ID
*GET,nmax,NODE,,NUM,MAX
N,nmax+10,0,piston_h-solid_h			!Create node on Keypoint #4
TSHAPE,pilo			!Pilot Targe element
E,nmax+10

! BEAM elements
TYPE,2
SECNUM,2
MAT,1
LSEL,S,LINE,,1,3,1		!Select the lines
LESIZE,ALL,ndiv1		!Refine mesh
LMESH,ALL			!Mesh lines
!/ESHAPE,1			!Show cross sections
!EPLOT

!------------------------------------------------------------------------------------------------------------

! >>>>> FRICTIONLESS CONTACTS <<<<<
! Applied on inside walls of cylinder against outer face of piston

! CONTACT ELEMENTS -> Piston Outer Walls	!CHECK NORMALS->they look opposite
! Remesh the piston side walls
VSEL,S,VOLU,,1,4,1		
ASLV,S
CLOCAL,11,1,0,0,0,0,-90
CSYS,11
ASEL,R,LOC,X,solid_r			! Select outer surfaces
nsla,,1
TYPE,5
REAL,2					! Select real constants set #2
esurf
ALLS

! TARGET ELEMENTS -> Cylinder Inner Wall
CSYS,0
VSEL,S,VOLU,,1,4,1
VSEL,INVE
ASLV,S
ASEL,R,LOC,Y,piston_h-solid_h-eps,piston_h+cylinder_hi-solid_h+eps
CSYS,11
ASEL,R,LOC,X,solid_r-eps,solid_r+eps	! Isolate inner areas
NSLA,S,1
CSYS,0
NSEL,R,LOC,Y,piston_h-solid_h-eps,piston_h+eps

TYPE,4
REAL,2					! Real Constant Set ID
!TSHAP,TRI6				! Targe 170 Segment Type: CYLI=cylinder, TRI6=triangular 6 nodes
ESURF				! Mesh NODES
alls
!------------------------------------------------------------------------------------------------------------

! >>>>> BOUNDARY CONDITIONS <<<<<

! ##### TOP FACE ##### 

ET,6,MASS21
R,3,0,0,0,0,0,0
KEYOPT,6,3,0
TYPE,6
REAL,3
alls
*GET, max_node, NODE, 0, NUM, MAX
masternode = max_node+10
N,masternode,0,35+(cylinder_ho+solid_base_y),0 					! Create 'master' node in the hook center
E, masternode									! Create point mass
NSEL,S,LOC,Y,cylinder_ho+solid_base_y-eps,cylinder_ho+solid_base_y+eps		! Pick top face nodes
NSEL,A,NODE,,masternode								! Add masternode
CERIG, masternode, ALL, ALL
ALLS

D,masternode,UX,0,,,,UY,UZ,ROTX,ROTY						! Keep ROTZ free, lock others


! ##### WHEEL AXLES #####

WPCSYS,,0					! Go back to origin WP
WPROTA,0,-10,0					! Rotate working plane
CSWPLA,100,0					! Rotated coordinate system 10Â°
NSEL,S,LOC,Y,-eps,+eps				! Select nodes on base
NSEL,R,LOC,X,65,270
D,ALL,UX					! Horizontal constrain
NROTAT,ALL					! Rotate nodal in new coordinate system
D,ALL,UY					! Inclined Constrain Nodes 
NSEL,S,LOC,Y,-eps,+eps
NSEL,R,LOC,X,-65,-270
D,ALL,UX					! Horizontal constrain
NROTAT,ALL					! Rotate nodal in new coordinate system
D,ALL,UY					! Inclined Constrain Nodes 
CSYS,0

						
! ##### INTERNAL PRESSURE #####

! Select Chamber Nodes
ALLS
CSYS,11
NSEL,S,LOC,X,0,0.5*cylinder_di+eps
CSYS,0
NSEL,R,LOC,Y,piston_h-eps,eps+piston_h+cylinder_hi-solid_h

! Apply loads
SF,ALL,PRES,press				! Surface load on selected nodes

!------------------------------------------------------------------------------------------------------------

! >>>>> SOLUTION <<<<<

/SOLU
ANTYPE,STATIC
alls
solve

!------------------------------------------------------------------------------------------------------------

! >>>>> POST-PROCESSING <<<<<
























