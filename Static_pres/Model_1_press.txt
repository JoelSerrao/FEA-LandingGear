! ---------------------------------------------------------------------------------
! STATIC_1: INTERNAL PRESSURE ANALYSIS
! ...............................................................
! PROBLEM:  DETERMINE STRAIN AND STRESS FIELD DUE TO INTERNAL PRESSURE IN CYLINDER
! ...............................................................
! MODEL:    HYBRID ELEMENTS SIMPLIFIED MODEL - 3D all round
! ---------------------------------------------------------------------------------
! version: 1_30-05

FINISH
/CLEAR, START, NEW
/FILNAM, static_pres
/TITLE, static internal pressure


! >>>>> MODEL PARAMETERS <<<<<

cylinder_do = 165		! Cylinder outer diameter [mm]
cylinder_di = 120		! Cylinder inner diameter [mm]
cylinder_ho = 880		! Cylinder outer height   [mm]
cylinder_hi = 520		! Cylinder inner height   [mm]

solid_h = 90		! Height of solid part of the piston   [mm]
solid_d = 120        ! Diameter of solid part of the piston [mm]
piston_h   = 690		! Piston full height [mm]
piston_b   = 265		! Piston cantilever length (one side) [mm]

side_support_r = 190	! Distance from axis of support [mm]
side_support_h = 400	! Distance of support from base [mm]

esize_s = 5		! Solid mesh element size
esize_b = 5		! Beam mesh size
ndiv    = 5		! Lines divisions size
!press = 				! Internal pressure value [Pa]

! >>>>> MATERIAL PROPERTIES <<<<<
/PREP7

! MATERIAL 1 -> High Res. Steel 4340M
MP,EX,1,210000
MP,PRXY,1,0.3

! >>>>> ELEMENT PROPERTIES <<<<<

! ELEMENT 1 -> SOLID185
ET,1,SOLID185
!KEYOPT,1,

! ELEMENT 2 -> BEAM188
ET,2,BEAM188
KEYOPT,2,3,2		!Quadratic formulation to represent correctly linearly varying bending moments
SECTYPE,2,BEAM,CSOLID
SECDATA,solid_d*0.5

! ELEMENT 3 -> CONTACT
ET,3,CONTA174
KEYOPT,3,2,2		!Contact algorithm: Multipoint constraint (MPC)
KEYOPT,3,12,5 		!Behavior of contact surface: Bonded (always)
KEYOPT,3,4,2 		!Rigid surface constraint

! ELEMENT 4 -> TARGET
ET,4,TARGE170
KEYOPT,4,2,1		 !Boundary conditions for rigid target nodes: Specified by user
R,2


! >>>>> BUILD GEOMETRIES <<<<<

! Base beam
K,1,0,0,0
K,2,+piston_b,0,0
K,3,-piston_b,0,0
solid_base_y = piston_h - solid_h
K,4,0,solid_base_y,0

L,1,2
L,1,3
L,1,4

! Solid head
solid_r = solid_d*0.5

WPOFFS,0,solid_base_y,0
WPROTA,0,-90
CYLIND, 0, solid_r, 0, solid_h, 360, 360			!#1
CYLIND, solid_r, cylinder_do*0.5, 0, 520, 360, 360		!#2
CYLIND, 0, cylinder_do*0.5, 520, 880, 360, 360			!#3
VADD,2,3								!Merge volumes 2,3
NUMCMP,VOLU								!Now they are #1 and #2 (vsel,s,volu,,2) to see them separately

! >>>>> MESH ELEMENTS <<<<<

! SOLID Elements
TYPE,1
ESIZE,esize_s
MSHKEY,0	!Free meshing (default), 1 for mapped
MSHAPE,0,3D	!Quadrilateral shaped elements
VMESH,1
VMESH,2

! CONTA on solid surface
TYPE,3
REAL,3
CSYS,0
VSEL,S,VOLU,,1
NSEL,S,LOC,Y,solid_base_y
NSLV,R,1			!Select lower face of piston
ESURF
ALLS

! PILOT node 
TYPE,4
REAL,3
*GET,nmax,NODE,,NUM,MAX
NKPT,nmax+1,4			!Create node on Keypoint #4
TSHAPE,pilo			!Pilot Targe element
E,nmax+1

! BEAM elements

TYPE,2
SECNUM,2
!MAT,1
LSEL,S,LINE,,1,3,1		!Select the lines
LESIZE,ALL,ndiv			!Refine mesh
LMESH,ALL			!Mesh lines
/ESHAPE,1
EPLOT

! >>>>> FRICTIONLESS CONTACTS <<<<<
! Applied on inside walls of cylinder

! >>>>> BOUNDARY CONDITIONS <<<<<

! INTERNAL PRESSURE
! CONSTRAINTS
























