! ---------------------------------------------------------------------------------
! STATIC_1: INTERNAL PRESSURE ANALYSIS
! ...............................................................
! PROBLEM:  DETERMINE STRAIN AND STRESS FIELD DUE TO INTERNAL PRESSURE IN CYLINDER
! ...............................................................
! MODEL:    HYBRID ELEMENTS SIMPLIFIED MODEL - 3D all round
! ---------------------------------------------------------------------------------
! version: v6_12/07

FINISH
/CLEAR, START, NEW
/FILNAM, static_pres_FINAL
/TITLE, static internal pressure

!------------------------------------------------------------------------------------------------------------

! >>>>> MODEL PARAMETERS <<<<<

eps = 1e-4

cylinder_do = 165		! Cylinder outer diameter [mm]
cylinder_di = 120		! Cylinder inner diameter [mm]
cylinder_ho = 880		! Cylinder outer height   [mm]
cylinder_hi = 520		! Cylinder inner height   [mm]

solid_h    = 90			! Height of solid part of the piston   [mm]
solid_d    = 120        	! Diameter of solid part of the piston [mm]
piston_h   = 690		! Piston full height [mm]
piston_b   = 265		! Piston cantilever length (one side) [mm]

side_support_r = 190		! Distance from axis of support [mm]
side_support_h = 370		! Distance of support from base [mm]

esize_s1 = 10			! Solid mesh element size #1
esize_s2 = 20			! Solid mesh element size #2
esize_b  = 20			! Beam mesh size
ndiv1    = 10			! Lines divisions size
ndiv2    = 5
press    = 2.2			! Internal pressure value [MPa]

!------------------------------------------------------------------------------------------------------------

! >>>>> MATERIAL PROPERTIES <<<<<
/PREP7

! MATERIAL 1 -> High Res. Steel 4340M
MP,EX,1,210000
MP,PRXY,1,0.3

!------------------------------------------------------------------------------------------------------------

! >>>>> ELEMENT PROPERTIES <<<<<

! ELEMENT 1 -> SOLID185(for mapped) 187 (for free)
ET,1,SOLID187
!KEYOPT,1,

! ELEMENT 2 -> BEAM188
ET,2,BEAM188
KEYOPT,2,3,2		!Quadratic formulation to represent correctly linearly varying bending moments
SECTYPE,2,BEAM,CSOLID
SECDATA,solid_d*0.5

! ELEMENT 3 -> CONTACT BONDED
ET,3,CONTA174
KEYOPT,3,2,2		!Contact algorithm: 2-Multipoint constraint (MPC)
KEYOPT,3,12,5 		!Behavior of contact surface: Bonded (always)
KEYOPT,3,4,2 		!Rigid surface constraint

! ELEMENT 4 -> TARGET
ET,4,TARGE170
KEYOPT,4,2,1		!Boundary conditions for rigid target nodes: Specified by user (0=autoset)
R,1			!Real Constants set #1, All Default

! ELEMENT 5 -> CONTACT FRICTIONLESS
ET,5,CONTA174
KEYOPT,5,1,0		!Degrees of freedom UX UY UZ
KEYOPT,5,2,0		!Contact algorithm: 0-Augmented Lagrangian
KEYOPT,5,4,2 		!Local Detection Point 0-Gaussian Point, 2-On nodal point normal to surf
!KEYOPT,5,9,0		!Effect of initial penetration or gap: 0-Include, 1-Exclude
KEYOPT,5,12,2		!Behavior of contact surface: No separation (Sliding Permitted) (NOT FOR GENERAL CONTACT)
KEYOPT,5,18,0		!Sliding behaviour; 0-Finite sliding, 1-Small sliding
R,2

!------------------------------------------------------------------------------------------------------------

! >>>>> BUILD GEOMETRIES <<<<<

! Base beam
K,1,0,0,0
K,2,+piston_b,0,0
K,3,-piston_b,0,0
solid_base_y = piston_h - solid_h
K,4,0,solid_base_y,0

L,1,2
L,1,3
L,1,4

! Solid head
solid_r = solid_d*0.5

K,5,solid_r,solid_base_y,0
K,6,solid_r,solid_base_y+solid_h
K,7,0,solid_base_y+solid_h
L,4,5
L,5,6
L,6,7
L,7,4
LESIZE,4,esize_s1
LESIZE,5,esize_s1
LESIZE,6,esize_s1
LESIZE,7,esize_s1

! Area 1
AL,4,5,6,7

K,8,solid_r,solid_base_y,0
K,9,cylinder_do*0.5,solid_base_y,0
K,10,cylinder_do*0.5,cylinder_hi+solid_base_y+40
K,11,cylinder_do*0.5,cylinder_ho+solid_base_y
K,12,0,cylinder_ho+solid_base_y
K,13,0,cylinder_hi+solid_base_y+40
K,14,0,cylinder_hi+solid_base_y
K,15,cylinder_di*0.5,cylinder_hi+solid_base_y

! horizontals
L,8,9
L,9,10
L,10,11
L,11,12
L,12,13
L,13,14
L,14,15
L,15,8
L,13,10
ALLS

AL,8,9,16,13,14,15
AL,10,11,12,16
VROTAT,1,,,,,,4,7
VSEL,S,VOLU,,1,4
VADD,ALL
NUMCMP,VOLU
ALLS

! Create volumes, add them to get only 3 of them and glue the top ones. Numcmp to have 1,2,3 as id
VROTAT,2,3,,,,,4,7
VSEL,S,VOLU,,1
VSEL,INVE
VSEL,R,LOC,Y,solid_base_y-eps,solid_base_y+eps+cylinder_hi
VADD,ALL
NUMCMP,VOLU
VSEL,S,VOLU,,1
VSEL,INVE
VSEL,R,LOC,Y,solid_base_y-eps+cylinder_hi,solid_base_y+eps+cylinder_ho
VADD,ALL
NUMCMP,VOLU
ALLS
VGLUE,2,3	!Glue together volumes
ALLS

! Mesh piston head
TYPE,1
ESIZE,esize_s1
MSHKEY,0
MSHAPE,1,3D 		!Tetra Elements if you are using solid187
VSEL,S,VOLU,,1		!Select volumes that make up the piston head
VMESH,ALL

! Select only chamber walls and mesh finely
VSEL,INVE
VSEL,R,LOC,Y,solid_base_y-eps,solid_base_y+eps+cylinder_hi
VMESH,ALL
ALLS

! Mesh roughly the rest
ESIZE,esize_s2
VSEL,S,LOC,Y,solid_base_y-eps+cylinder_hi,solid_base_y+eps+cylinder_ho
VMESH,ALL
ALLS

! I checked the interface nodes and they are shared as they should 

! -------------------------------------------------------------------------------

! >>>>> BEAM-SOLID INTERFACE <<<<<

! CONTA on solid surface
TYPE,3
REAL,1				!Call real constants ID
CSYS,0
VSEL,S,VOLU,,1
NSLV,S,1
NSEL,R,LOC,Y,solid_base_y-eps,solid_base_y+eps
CSYS,5
NSEL,R,LOC,X,0,solid_r-eps !*0.9
CSYS,0
ESURF
ALLS

! PILOT node 
TYPE,4
REAL,1				!Call real constants ID
*GET,nmax,NODE,,NUM,MAX
N,nmax+1,0,piston_h-solid_h			!Create node on Keypoint #4
TSHAPE,pilo			!Pilot Targe element
E,nmax+1

! BEAM elements
TYPE,2
SECNUM,2
MAT,1
NKPT,nmax+2,1
!NKPT,nmax+3,2
!NKPT,nmax+4,3
E,nmax+1,nmax+2
!E,nmax+2,nmax+3
!E,nmax+2,nmax+4
LSEL,S,LINE,,1,2,1		!Select the lines
LESIZE,ALL,ndiv1		!Refine mesh
LMESH,ALL			!Mesh lines
!/ESHAPE,1			!Show cross sections
!EPLOT
ALLS
NSEL,S,LOC,Y,-eps,+eps
NUMMRG,NODES
ALLS
!------------------------------------------------------------------------------------------------------------

! >>>>> FRICTIONLESS CONTACTS <<<<<
! Applied on inside walls of cylinder against outer face of piston

! CONTACT ELEMENTS -> Piston Outer Walls	!CHECK NORMALS->they look opposite
! Remesh the piston side walls
VSEL,S,VOLU,,1		
ASLV,S
CLOCAL,11,1,0,0,0,0,-90
CSYS,11
ASEL,R,LOC,X,solid_r			! Select outer surfaces
NSLA,,1
TYPE,5
REAL,2					! Select real constants set #2
ESURF
ALLS

! TARGET ELEMENTS -> Cylinder Inner Wall
CSYS,0
VSEL,S,VOLU,,1
VSEL,INVE
ASLV,S
ASEL,R,LOC,Y,piston_h-solid_h-eps,piston_h+cylinder_hi-solid_h+eps
CSYS,11
ASEL,R,LOC,X,solid_r-eps,solid_r+eps	! Isolate inner areas
NSLA,S,1
CSYS,0
NSEL,R,LOC,Y,piston_h-solid_h-eps,piston_h+eps

TYPE,4
REAL,2					! Real Constant Set ID
ESURF					! Mesh NODES
ALLS

!------------------------------------------------------------------------------------------------------------

! >>>>> BOUNDARY CONDITIONS <<<<<

! ##### TOP FACE ##### 

ET,6,184			!MPC184
KEYOPT,6,1,0			! Set option for rigid link behavior 0, beam 1
TYPE,6

*GET, max_node, NODE, 0, NUM, MAX
masternode = max_node+10
N,masternode,0,35+(cylinder_ho+solid_base_y),0 					! Create 'master' node in the hook center

NSEL,S,LOC,Y,cylinder_ho+solid_base_y-eps,cylinder_ho+solid_base_y+eps		! Pick top face nodes
*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
     *GET, n_node, NODE, 0, NUM, MAX
     E,masternode,n_node
     NSEL,U,NODE,,n_node
*ENDDO
ALLS

D,masternode,UX,0,,,,UY,UZ,ROTX,ROTY						! Keep ROTZ free, lock others


! ##### STEERING POINTS #####

! Create master nodes
ALLS
CSYS,0
*GET, max_node, NODE, 0, NUM, MAX
masternode = max_node+10
N,masternode,side_support_r,solid_base_y+side_support_h,0	
N,masternode+10,-side_support_r,solid_base_y+side_support_h,0

! Select Wall Nodes (DX)
ALLS
NSEL,S,LOC,Y,solid_base_y+side_support_h-55,solid_base_y+side_support_h+80	! Select in Y
NSEL,R,LOC,Z,-50,+50								! Select in Z
NSEL,R,LOC,X,0,cylinder_do*0.5+eps						! Reselect in X		
CSYS,5
NSEL,R,LOC,X,cylinder_do*0.5-eps,cylinder_do*0.5+eps				! Reselect in X		
CSYS,0

*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
     *GET, n_node, NODE, 0, NUM, MAX
     E,masternode,n_node
     NSEL,U,NODE,,n_node
*ENDDO
ALLS

D,masternode,ux,0,,,,uy,uz,rotx,rotz

! Select Wall Nodes (SX)
ALLS
NSEL,S,LOC,Y,solid_base_y+side_support_h-55,solid_base_y+side_support_h+80	! Select in Y
NSEL,R,LOC,Z,-50,+50								! Select in Z
NSEL,R,LOC,X,0,-cylinder_do*0.5-eps						! Reselect in X		
CSYS,5
NSEL,R,LOC,X,cylinder_do*0.5-eps,cylinder_do*0.5+eps				! Reselect in X	
CSYS,0	

*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
     *GET, n_node, NODE, 0, NUM, MAX
     E,masternode+10,n_node
     NSEL,U,NODE,,n_node
*ENDDO
ALLS
	
D,masternode+10,UX,0,,,,UY,UZ,ROTX,ROTZ


! ##### WHEEL AXLES #####

ALLS
CSYS,0
*GET, max_node, NODE, 0, NUM, MAX
masternode = max_node+10
N,masternode,-(65+270)*0.5,-20,0	
N,masternode+10,0.5*(270+65),-20,0

! Select Beam Nodes (DX)
ALLS
NSEL,S,LOC,Y,-eps,+eps								! Select in Y
NSEL,R,LOC,X,65,270								! Reselect in X		

TYPE,6

*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
     *GET, n_node, NODE, 0, NUM, MAX
     E,masternode+10,n_node
     NSEL,U,NODE,,n_node
*ENDDO
ALLS

! Select Beam Nodes (SX)
ALLS
NSEL,S,LOC,Y,-eps,+eps								! Select in Y
NSEL,R,LOC,X,-270,-65								! Reselect in X		

*GET, num_nodes, NODE, 0, COUNT
*DO, ii, 1, num_nodes,1
     *GET, n_node, NODE, 0, NUM, MAX
     E,masternode,n_node
     NSEL,U,NODE,,n_node
*ENDDO
ALLS

! Constrain Masternodes
WPCSYS,,0					! Go back to origin WP
WPROTA,0,-10,0					! Rotate working plane
CSWPLA,100,0					! Rotated coordinate system 10Â°

NSEL,S,NODE,,masternode				
NSEL,A,NODE,,masternode+10		
D,ALL,UX,0,,,,UZ,ROTX,ROTY,ROTZ			! Horizontal constrain
NROTAT,ALL					! Rotate nodal in new coordinate system
D,ALL,UY					! Inclined Constrain on Nodes 
ALLS
					
! ##### INTERNAL PRESSURE #####

! Select Chamber Nodes
ALLS
CSYS,11
NSEL,S,LOC,X,0,0.5*cylinder_di+eps
CSYS,0
NSEL,R,LOC,Y,piston_h-eps,eps+piston_h+cylinder_hi-solid_h

! Apply loads
SF,ALL,PRES,press				! Surface load on selected nodes
ALLS
!------------------------------------------------------------------------------------------------------------

! >>>>> SOLUTION <<<<<

/SOLU
ANTYPE,STATIC
ALLS
! The warning are auto solved by ansys
!warning1: NLGEOM,ON		!Add to include large deflection effects
!warning2: NSUB or DELTIM
SOLVE

!------------------------------------------------------------------------------------------------------------

! >>>>> GENERAL POST-PROCESSING <<<<<
/POST1
!PLDISP,2	!Deformed Shape -> the volumes are ok but the beams are not deforming
! USE GUI








